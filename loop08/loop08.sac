/*******************************************************************************
 *
 * Livermore Loop no 8
 *
 * ADI integration
 *
 *******************************************************************************/

use StdIO: all;
use Array: all;
use RTimer: all;


#ifndef VARIANT
#define VARIANT C
#endif

#ifndef INLINE
#define LL_INLINE
#else
#define LL_INLINE inline
#endif

#define APPEND(v) Loop8_ ## v
#define FULL(v) APPEND(v)
#define LOOP FULL( VARIANT)

#define STR(s) #s
#define STRING(s) STR(s)


#ifndef SIZE0
#define SIZE0 2
#endif

#ifndef SIZE1
#define SIZE1 101
#endif

#ifndef SIZE2
#define SIZE2 5
#endif

LL_INLINE
double[.,.,.], double[.,.,.], double[.,.,.]
Loop8_C( int n, double[10] consts, double[SIZE0,SIZE1,SIZE2] u1,
                                   double[SIZE0,SIZE1,SIZE2] u2,
                                   double[SIZE0,SIZE1,SIZE2] u3)
{
  a11=consts[0];
  a12=consts[1];
  a13=consts[2];
  a21=consts[3];
  a22=consts[4];
  a23=consts[5];
  a31=consts[6];
  a32=consts[7];
  a33=consts[8];
  sig=consts[9];
  nl1 = 0;
  nl2 = 1;
  du1 = genarray( [n], 0d);
  du2 = du1;
  du3 = du1;
  for ( kx=1 ; kx<3 ; kx++ ){
    for ( ky=1 ; ky<n ; ky++ ) {
      du1[ky] = u1[nl1,ky+1,kx] - u1[nl1,ky-1,kx];
      du2[ky] = u2[nl1,ky+1,kx] - u2[nl1,ky-1,kx];
      du3[ky] = u3[nl1,ky+1,kx] - u3[nl1,ky-1,kx];
      u1[nl2,ky,kx]= u1[nl1,ky,kx]+a11*du1[ky]+a12*du2[ky]+a13*du3[ky] + sig*
                     (u1[nl1,ky,kx+1]-2.0*u1[nl1,ky,kx]+u1[nl1,ky,kx-1]);
      u2[nl2,ky,kx]= u2[nl1,ky,kx]+a21*du1[ky]+a22*du2[ky]+a23*du3[ky] + sig*
                     (u2[nl1,ky,kx+1]-2.0*u2[nl1,ky,kx]+u2[nl1,ky,kx-1]);
      u3[nl2,ky,kx]= u3[nl1,ky,kx]+a31*du1[ky]+a32*du2[ky]+a33*du3[ky] + sig*
                     (u3[nl1,ky,kx+1]-2.0*u3[nl1,ky,kx]+u3[nl1,ky,kx-1]);
    }
  }
  return (u1,u2,u3);
}

LL_INLINE
double[.,.,.], double[.,.,.], double[.,.,.]
Loop8_NAIVE( int n, double[10] consts, double[SIZE0,SIZE1,SIZE2] u1,
                                       double[SIZE0,SIZE1,SIZE2] u2,
                                       double[SIZE0,SIZE1,SIZE2] u3)
{

  a11=consts[0];
  a12=consts[1];
  a13=consts[2];
  a21=consts[3];
  a22=consts[4];
  a23=consts[5];
  a31=consts[6];
  a32=consts[7];
  a33=consts[8];
  sig=consts[9];
  nl1 = 0;
  nl2 = 1;

  du1 = with {
          (. <= [ky,kx] <= .) : u1[nl1,ky+2,kx+1] - u1[nl1,ky,kx+1];
        } : genarray( [n-1,2], 0d);
  du2 = with {
          (. <= [ky,kx] <= .) : u2[nl1,ky+2,kx+1] - u2[nl1,ky,kx+1];
        } : genarray( [n-1,2], 0d);
  du3 = with {
          (. <= [ky,kx] <= .) : u3[nl1,ky+2,kx+1] - u3[nl1,ky,kx+1];
        } : genarray( [n-1,2], 0d);
  u1 = with {
         ([nl2,1,1] <= [kz,ky,kx] < [nl2+1,n,3]) 
         : u1[nl1,ky,kx]+a11*du1[ky-1,kx-1]+a12*du2[ky-1,kx-1]+a13*du3[ky-1,kx-1] + sig*
                     (u1[nl1,ky,kx+1]-2.0*u1[nl1,ky,kx]+u1[nl1,ky,kx-1]);
       } : modarray( u1);
  u2 = with {
         ([nl2,1,1] <= [kz,ky,kx] < [nl2+1,n,3]) 
         : u2[nl1,ky,kx]+a21*du1[ky-1,kx-1]+a22*du2[ky-1,kx-1]+a23*du3[ky-1,kx-1] + sig*
                     (u2[nl1,ky,kx+1]-2.0*u2[nl1,ky,kx]+u2[nl1,ky,kx-1]);
       } : modarray( u2);
  u3 = with {
         ([nl2,1,1] <= [kz,ky,kx] < [nl2+1,n,3]) 
         : u3[nl1,ky,kx]+a31*du1[ky-1,kx-1]+a32*du2[ky-1,kx-1]+a33*du3[ky-1,kx-1] + sig*
                     (u3[nl1,ky,kx+1]-2.0*u3[nl1,ky,kx]+u3[nl1,ky,kx-1]);
       } : modarray( u3);
  return (u1,u2,u3);
}

LL_INLINE
double[.,.,.], double[.,.,.], double[.,.,.]
Loop8_NAIVE2( int n, double[10] consts, double[SIZE0,SIZE1,SIZE2] u1,
                                        double[SIZE0,SIZE1,SIZE2] u2,
                                        double[SIZE0,SIZE1,SIZE2] u3)
{

  a11=consts[0];
  a12=consts[1];
  a13=consts[2];
  a21=consts[3];
  a22=consts[4];
  a23=consts[5];
  a31=consts[6];
  a32=consts[7];
  a33=consts[8];
  sig=consts[9];
  nl1 = 0;
  nl2 = 1;

  for ( kx=1 ; kx<3 ; kx++ ){
    du1 = with {
            (. <= [ky] <= .) : u1[nl1,ky+2,kx] - u1[nl1,ky,kx];
          } : genarray( [n-1], 0d);
    du2 = with {
            (. <= [ky] <= .) : u2[nl1,ky+2,kx] - u2[nl1,ky,kx];
          } : genarray( [n-1], 0d);
    du3 = with {
            (. <= [ky] <= .) : u3[nl1,ky+2,kx] - u3[nl1,ky,kx];
          } : genarray( [n-1], 0d);
    u1 = with {
           ([nl2,1,kx] <= [kz,ky,kx] < [nl2+1,n,kx+1])
           : u1[nl1,ky,kx]+a11*du1[ky-1]+a12*du2[ky-1]+a13*du3[ky-1] + sig*
                       (u1[nl1,ky,kx+1]-2.0*u1[nl1,ky,kx]+u1[nl1,ky,kx-1]);
         } : modarray( u1);
    u2 = with {
           ([nl2,1,kx] <= [kz,ky,kx] < [nl2+1,n,kx+1])
           : u2[nl1,ky,kx]+a21*du1[ky-1]+a22*du2[ky-1]+a23*du3[ky-1] + sig*
                       (u2[nl1,ky,kx+1]-2.0*u2[nl1,ky,kx]+u2[nl1,ky,kx-1]);
         } : modarray( u2);
    u3 = with {
           ([nl2,1,kx] <= [kz,ky,kx] < [nl2+1,n,kx+1])
           : u3[nl1,ky,kx]+a31*du1[ky-1]+a32*du2[ky-1]+a33*du3[ky-1] + sig*
                       (u3[nl1,ky,kx+1]-2.0*u3[nl1,ky,kx]+u3[nl1,ky,kx-1]);
         } : modarray( u3);
  }
  return (u1,u2,u3);
}

LL_INLINE
double[.,.,.], double[.,.,.], double[.,.,.]
Loop8_APL( int n, double[10] consts, double[SIZE0,SIZE1,SIZE2] u1,
                                     double[SIZE0,SIZE1,SIZE2] u2,
                                     double[SIZE0,SIZE1,SIZE2] u3)
{
  a11=consts[0];
  a12=consts[1];
  a13=consts[2];
  a21=consts[3];
  a22=consts[4];
  a23=consts[5];
  a31=consts[6];
  a32=consts[7];
  a33=consts[8];
  sig=consts[9];
  nl1 = 0;
  nl2 = 1;

  du1 = shift([0,-1,0], u1) - shift([0,1,0], u1);
  du2 = shift([0,-1,0], u2) - shift([0,1,0], u2);
  du3 = shift([0,-1,0], u3) - shift([0,1,0], u3);

  /*
   * Note here that we compute over the entire 5 elements in the thirdaxis , rather than
   * only the two above....
   */
  u1[nl2] = u1[nl1] + a11*du1[nl1] + a12*du2[nl1] + a13*du3[nl1] 
            + sig * ( shift( [0,0,-1], u1)[nl1] - 2.0*u1[nl1] + shift( [0,0,1], u1)[nl1]);
  u2[nl2] = u2[nl1] + a21*du1[nl1] + a22*du2[nl1] + a23*du3[nl1] 
            + sig * ( shift( [0,0,-1], u2)[nl1] - 2.0*u2[nl1] + shift( [0,0,1], u2)[nl1]);
  u3[nl2] = u3[nl1] + a31*du1[nl1] + a32*du2[nl1] + a33*du3[nl1] 
            + sig * ( shift( [0,0,-1], u3)[nl1] - 2.0*u3[nl1] + shift( [0,0,1], u3)[nl1]);

  return (u1,u2,u3);
}

LL_INLINE
double[.,.,.], double[.,.,.], double[.,.,.]
Loop8_APL2( int n, double[10] consts, double[SIZE0,SIZE1,SIZE2] u1,
                                      double[SIZE0,SIZE1,SIZE2] u2,
                                      double[SIZE0,SIZE1,SIZE2] u3)
{
  a11=consts[0];
  a12=consts[1];
  a13=consts[2];
  a21=consts[3];
  a22=consts[4];
  a23=consts[5];
  a31=consts[6];
  a32=consts[7];
  a33=consts[8];
  sig=consts[9];
  nl1 = 0;
  nl2 = 1;

  u1p = u1[nl1];
  u2p = u2[nl1];
  u3p = u3[nl1];

  du1 = shift([-1,0], u1p) - shift([1,0], u1p);
  du2 = shift([-1,0], u2p) - shift([1,0], u2p);
  du3 = shift([-1,0], u3p) - shift([1,0], u3p);

  /*
   * Note here that we compute over the entire 5 elements in the thirdaxis , rather than
   * only the two above....
   */
  u1[nl2] = u1p + a11*du1 + a12*du2 + a13*du3
            + sig * ( shift( [0,-1], u1p) - 2.0*u1p + shift( [0,1], u1p));
  u2[nl2] = u2p + a21*du1 + a22*du2 + a23*du3
            + sig * ( shift( [0,-1], u2p) - 2.0*u2p + shift( [0,1], u2p));
  u3[nl2] = u3p + a31*du1 + a32*du2 + a33*du3
            + sig * ( shift( [0,-1], u3p) - 2.0*u3p + shift( [0,1], u3p));

  return (u1,u2,u3);
}

inline double[.] genArray(int size)
{
  A = tod( 1 + iota(size));
  return(A);
}


int main ()
{
  double[ SIZE0, SIZE1, SIZE2] u1;
  double[ SIZE0, SIZE1, SIZE2] u2;
  double[ SIZE0, SIZE1, SIZE2] u3;

  timer = createRTimer();

  rep = (int) FibreScanIntArray();
  n = SIZE1-1;
  consts = [1.2, 5.6, 8.1, 9.8, 1.5, 4.0, 0.5, 7.9, 6.4, 3.9];

  u1 = reshape( [SIZE0,SIZE1,SIZE2], genArray( SIZE0*SIZE1*SIZE2));
  u2 = u1;
  u3 = u1;

  printf( "Running " STRING( LOOP) "\n");
  printf( "Number of repetitions: %d\n", rep);
  printf( "Iteration space size : %d\n", n);

  startRTimer( timer);
  for (i=0; i<rep; i++){
    u1, u2, u3 = LOOP( n, consts, u1, u2, u3);
  }
  stopRTimer( timer);

  printf( "Element [1] of the result is : %f\n", u1[1,1,1]);

  numFlops = tod(rep)*tod(2)*tod(n-1)*tod(36);

  printf( "Wall Clock Time spent           : %7.3f secs\n", getRTimerDbl( timer));
  printf( "Computer Performance achieved   : %7.3f GFlops/s\n", (tod(numFlops)/1000000000.0) / getRTimerDbl( timer));
  
  return( 0);
}

